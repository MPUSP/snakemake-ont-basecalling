# ----------------------------------------------------- #
# A Snakemake workflow for nanopore basecalling         #
# and multiplexing using dorado.                        #
# ----------------------------------------------------- #

import os
import pandas as pd
from snakemake.utils import validate
from snakemake.utils import min_version


# -----------------------------------------------------
# meta data and run info
# -----------------------------------------------------
__authors__ = ["Rina Ahmed-Begrich", "Michael Jahn"]

cyan = "\033[36m"
end = "\033[0m"

msg = f"""\nONT-basecalling: A Snakemake workflow
for nanopore basecalling and demultiplexing using dorado."""

prolog = f"Output directory: {os.getcwd()}/results"

epilog = f"""
Written by {", ".join(__authors__)}.
Max Planck Unit for the Science of Pathogens. Copyright (c) 2025.
"""

# -----------------------------------------------------
# check minimum snakemake version
# -----------------------------------------------------
min_version("8.24.1")
min_dorado_version = (1, 3, 0)


# -----------------------------------------------------
# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"


# -----------------------------------------------------
# container definition (optional)
# -----------------------------------------------------
container: "oras://ghcr.io/mpusp/snakemake-ont-basecalling:latest"


# -----------------------------------------------------
# load rules
# -----------------------------------------------------
include: "rules/common.smk"
include: "rules/basecall.smk"
include: "rules/demultiplex.smk"
include: "rules/qc.smk"


# check if dorado version is >= min_dorado_version
try:
    dorado_path = config["dorado"]["path"]
    version_tuple = check_dorado_version(dorado_path)
except Exception as e:
    raise SystemExit(f"{cyan}\nFailed to determine dorado version: {e} {end}\n")

# compare tuples (major, minor, patch)
if version_tuple >= min_dorado_version:
    print(
        f"{cyan}\n--- Detected dorado version: "
        f"{'.'.join(map(str, version_tuple))} ---{end}\n"
    )
else:
    raise SystemExit(
        f"{cyan}\n--- Detected dorado version "
        f"{'.'.join(map(str, version_tuple))} < 1.3.0. "
        f"Please update dorado to version >= 1.3.0 and re-run the workflow. ---{end}\n"
    )


# -----------------------------------------------------
# optional messages, log and error handling
# -----------------------------------------------------
onstart:
    print(f"{cyan}\n--- Start workflow! ---\n\n{prolog}\n\n{end}")


onsuccess:
    print(f"{cyan}\n{msg}\n{epilog}\n--- Workflow finished! ---\n\n{end}")


onerror:
    print(f"{cyan}\n{msg}\n{epilog}\n --- An error occurred! ---\n\n{end}")


localrules:
    download_model,
    dorado_demux,
    aggregrate_file,
    aggregrate_barcode,


# -----------------------------------------------------
# target rules
# -----------------------------------------------------
rule all:
    input:
        expand(
            "results/{run}/dorado_final/input_fastq.txt",
            run=runs.index,
        ),
        expand(
            "results/{run}/report/{tool}_report.html",
            run=runs.index,
            tool=config["report"]["tools"],
        ),
    default_target: True
